START_MARKER

dnl Note: This module has some tests with an external test script in
dnl src/modules/_Stdio

cond_begin( Crypto.Hash )

test_do( SSL.https )

test_do( add_constant( "S", String.string2hex ) )
test_eq( S(SSL.Cipher.MACsha("foo")->hash("bar")),
	 "2e6eda2d36d2a2f4b5d7c28c920a0b679f17d76c" )
test_eq( S(SSL.Cipher.MACmd5("foo")->hash("bar")),
         "c413fb96a80f160c25d0e61f5b5d8078" )
test_eq( S(SSL.Cipher.MAChmac_sha("foo")->hash("bar")),
         "46b4ec586117154dacd49d664e5d63fdc88efb51" )
test_eq( S(SSL.Cipher.MAChmac_md5("foo")->hash("bar")),
         "0c7a250281315ab863549f66cd8a3a53" )
test_eq( S(SSL.Cipher.MAChmac_sha256("foo")->hash("bar")),
         "f9320baf0249169e73850cd6156ded0106e2bb6ad8cab01b7bbbebe6d1065317" )
cond_resolv(Crypto.SHA384, [[
test_eq( S(SSL.Cipher.MAChmac_sha384("foo")->hash("bar")),
         "3d10d391bee2364df2c55cf605759373e1b5a4ca9355d8f3fe42970471eca2e422a79271a0e857a69923839015877fc6" )
]])
cond_resolv(Crypto.SHA512, [[
test_eq( S(SSL.Cipher.MAChmac_sha512("foo")->hash("bar")),
         "114682914c5d017dfe59fdc804118b56a3a652a0b8870759cf9e792ed7426b08197076bf7d01640b1b0684df79e4b67e37485669e8ce98dbab60445f0db94fce" )
]])

test_do([[
  array(int) parse_fields(array(string) fields, int fno,
			  mapping(string:mapping|int) tab)
  {
    while (fno < sizeof(fields)) {
      int|mapping val = tab[fields[fno++] ];
      if (undefinedp(val)) break;
      if (intp(val)) return ({ val, fno });
      tab = val;
    }
    return ({ -1, fno });
  };
  add_constant("parse_fields", parse_fields);
]])

test_tests([[
  import SSL.Constants;
  array(int) a()
  {
    // Validate the SSL.Constants.CIPHER_SUITES table.

    log_status("Testing the cipher suite lookup table...");

    int successes;
    int tests;

    foreach(indices(SSL.Constants), string sym) {
      if (!has_prefix(sym, "SSL_") && !has_prefix(sym, "TLS_")) continue;
      array(string) fields = sym/"_";
      int fno = 1;

      int ke = -1;

      [ke, fno] = parse_fields(fields, fno,
	([
	  "null":KE_null,
	  "rsa":KE_rsa,
	  "dh":([
	    "dss":KE_dh_dss,
	    "rsa":KE_dh_rsa,
	    "anon":KE_dh_anon,
	  ]),
	  "dhe":([
	    "dss":KE_dhe_dss,
	    "rsa":KE_dhe_rsa,
	  ]),
#if constant(Crypto.ECC.Curve)
	  "ecdh":([
	    "ecdsa":KE_ecdh_ecdsa,
	    "rsa":KE_ecdh_rsa,
	    "anon":KE_ecdh_anon,
	  ]),
	  "ecdhe":([
	    "ecdsa":KE_ecdhe_ecdsa,
	    "rsa":KE_ecdhe_rsa,
	  ]),
#endif
	]));

      if ((sizeof(fields) > fno) && (< "fips", "oldfips" >)[ fields[fno] ] &&
          (ke == KE_rsa)) {
        ke = KE_rsa_fips;
        fno++;
      }

      if ((sizeof(fields) > fno) &&
	  !(<"export", "with">)[fields[fno] ]) {
	// Probably something like "psk".
	ke = -1;
      }

      // Skip ahead to the cipher.
      while ((sizeof(fields) > fno) && (fields[fno++] != "with"))
	;

      int cipher = -1;
      [cipher, fno] = parse_fields(fields, fno,
	([
	  "null":CIPHER_null,
	  "rc4":([
	    "40":CIPHER_rc4_40,
	    "128":CIPHER_rc4,
	  ]),
#if constant(Crypto.Arctwo)
	  "rc2":([
	    "cbc":([
	      "40":CIPHER_rc2_40,
	    ]),
	  ]),
#endif
	  "des40":CIPHER_des40,
	  "des":CIPHER_des,
#if constant(Crypto.IDEA)
	  "idea":CIPHER_idea,
#endif
	  "3des":([
	    "ede":cipher = CIPHER_3des,
	  ]),
	  "aes":([
	    "128":CIPHER_aes,
	    "256":CIPHER_aes256,
	  ]),
#if constant(Crypto.Camellia)
	  "camellia":([
	    "128":CIPHER_camellia128,
	    "256":CIPHER_camellia256,
	  ]),
#endif
#if constant(Crypto.ChaCha20)
	  "chacha20":CIPHER_chacha20,
#endif
	]));
      int mode = -1;
      if (sizeof(fields) > fno) {
	switch(fields[fno]) {
	case "cbc":
	  fno++;
	  break;
	case "ccm":
	  fno++;
	  mode = MODE_ccm;
	  if ((sizeof(fields) > fno) &&
	      (fields[fno] == "8")) {
	    mode = MODE_ccm_8;
	    fno++;
	  }
	  break;
#if constant(Crypto.AES.GCM)
	case "gcm":
	  mode = MODE_gcm;
	  fno++;
	  break;
#endif
#if constant(Crypto.ChaCha20.POLY1305)
	case "poly1305":
	  mode = MODE_poly1305;
	  fno++;
	  break;
#endif
	}
      }
      int hash = -1;
      [hash, fno] = parse_fields(fields, fno,
	([
	  "null":HASH_none,
	  "md5":HASH_md5,
	  "sha":HASH_sha,
	  "sha256":HASH_sha256,
	  "sha384":HASH_sha384,
	  "sha512":HASH_sha512,
	]));
      array(int) expected = UNDEFINED;
      if ((hash == -1) && (< MODE_ccm, MODE_ccm_8 >)[mode]) {
	// RFC 6655 3.
	// RFC 7251 2.
	hash = HASH_sha256;
      }
      if ((ke >= 0) && (cipher >= 0) && (hash >= 0)) {
	if ((mode == -1) &&
	    ((hash > HASH_sha256) &&
	     !((cipher == CIPHER_camellia256) &&
	       has_prefix(sym, "TLS_ecdh")) ||
	     ((hash == HASH_sha256) &&
	      (cipher == CIPHER_aes) &&
	      has_prefix(sym, "TLS_ecdh")))) {
	  // NB: The RFC 6367 suites allow use with TLS 1.1 and earlier.
	  //     While the reverse is true for the RFC 5289 suites.
	  mode = MODE_cbc;
	}
	if (mode >= 0) {
	  expected = ({ ke, cipher, hash, mode });
	} else {
	  expected = ({ ke, cipher, hash });
	}
      }

      tests++;
      if (equal(CIPHER_SUITES[SSL.Constants[sym] ], expected)) {
	successes++;
      } else if (expected) {
	if (!CIPHER_SUITES[SSL.Constants[sym] ]) {
	  log_msg("Unimplemented cipher suite: %s\n", sym);
	} else {
	  log_msg("Cipher suite lookup table error for suite %s.\n"
		  "Got: ({ %{%d, %}}).\n"
		  "Exp: ({ %{%d, %})).\n",
		  sym, SSL.Constants.CIPHER_SUITES[SSL.Constants[sym] ],
		  expected);
	}
      } else {
	log_msg("Cipher suite %s not supported by test script.\n", sym);
      }
    }
    return ({ successes, tests - successes });
  }
]])

test_do([[
  add_constant("parse_fields");
]])

define(run_sub_test, [[
  test_tests([[
    array a() {return Tools.Testsuite.run_script ($1);}
  ]])
]])

dnl Displaced tests for SSL.File.
run_sub_test(({"SRCDIR../../../src/modules/_Stdio/async_tls_close_test.pike", "0", "0"}))
run_sub_test(({"SRCDIR../../../src/modules/_Stdio/async_tls_close_test.pike", "0", "1"}))
run_sub_test(({"SRCDIR../../../src/modules/_Stdio/async_tls_close_test.pike", "1", "0"}))
run_sub_test(({"SRCDIR../../../src/modules/_Stdio/async_tls_close_test.pike", "1", "1"}))

test_do([[
#define ASSERT(X) if(!(X)) error("Assertion failed.\n")

  object rsa = Crypto.RSA();
  rsa->set_random(random_string);
  rsa->generate_key( 1024 );
  string _key = Standards.PEM.build("RSA PRIVATE KEY",
    Standards.PKCS.RSA.private_key(rsa));

  string key = Standards.PEM.Messages(_key)->get_private_key();
  object rsa_again=Standards.PKCS.RSA.parse_private_key(key);

  ASSERT(rsa->public_key_equal(rsa_again));

  array attrs = ({
    (["commonName":"*"]),
    (["organizationName":"Test"]),
  });

  string _cert = Standards.PEM.build("CERTIFICATE",
    Standards.X509.make_selfsigned_certificate(rsa_again, 3600*24, attrs));

  add_constant("pem_key", _key);
  add_constant("pem_cert", _cert);

  // Same public key, but with SHA1.
  add_constant("cert_sha1",
    Standards.X509.make_selfsigned_certificate(rsa_again, 3600*24, attrs,
						UNDEFINED, Crypto.SHA1));

  // Same public key, but with MD5.
  add_constant("cert_md5",
    Standards.X509.make_selfsigned_certificate(rsa_again, 3600*24, attrs,
						UNDEFINED, Crypto.MD5));
]])

test_do([[
  class TestContext {
    inherit SSL.Context;

    int(0..1) expect_fail;
    SSL.Alert alert_factory(SSL.Connection con,
			    int level, int description,
			    SSL.Constants.ProtocolVersion version,
			    string|void message)
    {
      if (message && !expect_fail &&
          (description != SSL.Constants.ALERT_close_notify)) {
	log_msg("ALERT [%s: %d: %s]: %s",
	        SSL.Constants.fmt_version(version), level,
	        SSL.Constants.fmt_constant(description, "ALERT"), message);
      }
      return ::alert_factory(con, level, description, version, message);
    }
  };
  add_constant("TestContext", TestContext);
  SSL.Context ctx = TestContext();
  ctx->random = random_string;
  add_constant("server_ctx", ctx);
]])

test_do([[

#define ASSERT(X) if(!(X)) error("Assertion failed.\n")

  string cert = Standards.PEM.Messages( pem_cert )->get_certificate();
  ASSERT(cert);

  string key = Standards.PEM.Messages( pem_key )->get_private_key();
  ASSERT(key);

  object rsa = Standards.PKCS.RSA.parse_private_key(key);
  ASSERT(rsa->key_size()>512);

  object tbs = Standards.X509.decode_certificate(cert);
  ASSERT(tbs->public_key->pkc->public_key_equal(rsa));

  server_ctx->add_cert(rsa, ({ cert }));

  server_ctx->add_cert(rsa, ({ cert_sha1 }));

  server_ctx->add_cert(rsa, ({ cert_md5 }));

  // FIXME: Shouldn't this be an ephemeral key?
  //        It doesn't seem to be related to the certificate.
  server_ctx->short_rsa = Crypto.RSA();
  server_ctx->short_rsa->set_random(server_ctx->random);
  server_ctx->short_rsa->generate_key(512);

  // Make sure that all cipher suites are available server side.
  server_ctx->preferred_suites = server_ctx->get_suites(-1, 2);
]])

test_do([[
  object dsa = Crypto.DSA()->generate_key(1024, 160);
  mapping attrs = ([
    "organizationName" : "Test",
    "commonName" : "*",
  ]);
  string cert = Standards.X509.make_selfsigned_certificate(dsa,
    3600*24, attrs);

  server_ctx->add_cert(dsa, ({ cert }));

  cert = Standards.X509.make_selfsigned_certificate(dsa,
    3600*24, attrs, UNDEFINED, Crypto.SHA1);

  server_ctx->add_cert(dsa, ({ cert }));
]])

test_do([[
#if constant(Crypto.ECC.Curve)
  object ecdsa = Crypto.ECC.SECP_521R1.ECDSA()->
    set_random(random_string)->generate_key();
  mapping attrs = ([
    "organizationName" : "Test",
    "commonName" : "*",
  ]);
  string cert = Standards.X509.make_selfsigned_certificate(ecdsa,
    3600*24, attrs);

  server_ctx->add_cert(ecdsa, ({ cert }));

  cert = Standards.X509.make_selfsigned_certificate(ecdsa,
    3600*24, attrs, UNDEFINED, Crypto.SHA1);

  server_ctx->add_cert(ecdsa, ({ cert }));
#endif
]])

test_do([[
  add_constant("client_msg", random_string(65519));
]])

test_do([[
  add_constant("log_ssl_failure",
	       lambda(int cmin, int cmax, int smin, int smax,
		      string failure, array(int) suites)
	       {
		 log_msg("%s..%s client with %s..%s server failed.\n",
			 SSL.Constants.fmt_version(cmin),
			 SSL.Constants.fmt_version(cmax),
			 SSL.Constants.fmt_version(smin),
			 SSL.Constants.fmt_version(smax));
		 log_msg("Cipher_suite: %s",
			 SSL.Constants.fmt_cipher_suites(suites));
		 log_msg("%s", failure);
	       });
]])

test_do([[
  int TLS_1_0 = SSL.Constants.PROTOCOL_TLS_1_0;
  int TLS_1_1 = SSL.Constants.PROTOCOL_TLS_1_1;

  // Mapping from suite to the last TLS version it was valid in (if any).
  mapping obsoleted_suites = ([
#if 0
    // Duplicates. Listed further below under their SSL_* names.
    SSL.Constants.TLS_rsa_with_idea_cbc_sha: TLS_1_1,
    SSL.Constants.TLS_rsa_with_des_cbc_sha: TLS_1_1,
    SSL.Constants.TLS_dh_dss_with_des_cbc_sha: TLS_1_1,
    SSL.Constants.TLS_dh_rsa_with_des_cbc_sha: TLS_1_1,
    SSL.Constants.TLS_dhe_dss_with_des_cbc_sha: TLS_1_1,
    SSL.Constants.TLS_dhe_rsa_with_des_cbc_sha: TLS_1_1,
    SSL.Constants.TLS_dh_anon_with_des_cbc_sha: TLS_1_1,
#endif

    // Obsoleted in TLS 1.1:
    SSL.Constants.SSL_dhe_dss_export_with_des40_cbc_sha: TLS_1_0,
    SSL.Constants.SSL_dhe_rsa_export_with_des40_cbc_sha: TLS_1_0,
    SSL.Constants.SSL_dh_dss_export_with_des40_cbc_sha: TLS_1_0,
    SSL.Constants.SSL_dh_rsa_export_with_des40_cbc_sha: TLS_1_0,
    SSL.Constants.SSL_dh_anon_export_with_des40_cbc_sha: TLS_1_0,
    SSL.Constants.SSL_dh_anon_export_with_rc4_40_md5: TLS_1_0,
    SSL.Constants.SSL_rsa_export_with_rc4_40_md5: TLS_1_0,
    SSL.Constants.SSL_rsa_export_with_rc2_cbc_40_md5: TLS_1_0,
    SSL.Constants.SSL_rsa_export_with_des40_cbc_sha: TLS_1_0,
    SSL.Constants.TLS_ecdhe_rsa_with_null_sha: TLS_1_0,
    SSL.Constants.TLS_ecdhe_ecdsa_with_null_sha: TLS_1_0,
    SSL.Constants.TLS_ecdh_rsa_with_null_sha: TLS_1_0,
    SSL.Constants.TLS_ecdh_ecdsa_with_null_sha: TLS_1_0,
    SSL.Constants.TLS_ecdh_anon_with_null_sha: TLS_1_0,
    SSL.Constants.TLS_rsa_with_null_sha256: TLS_1_0,
    SSL.Constants.SSL_rsa_with_null_sha: TLS_1_0,
    SSL.Constants.SSL_rsa_with_null_md5: TLS_1_0,
    SSL.Constants.SSL_null_with_null_null: TLS_1_0,

    // Obsoleted in TLS 1.2:
    SSL.Constants.SSL_rsa_with_idea_cbc_md5: TLS_1_1,
    SSL.Constants.SSL_rsa_with_idea_cbc_sha: TLS_1_1,
    SSL.Constants.SSL_dhe_rsa_with_des_cbc_sha: TLS_1_1,
    SSL.Constants.SSL_dh_anon_with_des_cbc_sha: TLS_1_1,
    SSL.Constants.SSL_rsa_with_des_cbc_md5: TLS_1_1,
    SSL.Constants.SSL_rsa_with_des_cbc_sha: TLS_1_1,
    SSL.Constants.SSL_dh_rsa_with_des_cbc_sha: TLS_1_1,
    SSL.Constants.SSL_dh_dss_with_des_cbc_sha: TLS_1_1,
    SSL.Constants.SSL_dhe_dss_with_des_cbc_sha: TLS_1_1,
    SSL.Constants.SSL_rsa_fips_with_des_cbc_sha: TLS_1_1,
    SSL.Constants.SSL_rsa_oldfips_with_des_cbc_sha: TLS_1_1,
  ]);

  foreach(SSL.Constants.CIPHER_SUITES; int suite; array(int) def) {
    if (obsoleted_suites[suite]) continue;
    if ((sizeof(def) < 4) || (def[3] == SSL.Constants.MODE_cbc)) {
      // CBC and RC4 suites obsoleted in TLS 1.3.
      obsoleted_suites[suite] = SSL.Constants.PROTOCOL_TLS_1_2;
    }
    if (!(< SSL.Constants.KE_dhe_dss, SSL.Constants.KE_dhe_rsa, SSL.Constants.KE_dh_anon,
	    SSL.Constants.KE_ecdhe_ecdsa, SSL.Constants.KE_ecdhe_rsa, SSL.Constants.KE_ecdh_anon >)[def[0] ]) {
      // Only DHE and ECDHE are valid key exchanges in TLS 1.3.
      obsoleted_suites[suite] = SSL.Constants.PROTOCOL_TLS_1_2;
    }
  }

  add_constant("obsoleted_suites", obsoleted_suites);
]])

test_do([[
  import SSL.Constants;
  string f(SSL.Context server_ctx, SSL.Context client_ctx,
           string exp_data, int exp_suite, int exp_version,
	   SSL.Session|void session)
  {
    Stdio.File client_con = Stdio.File();
    Stdio.File server_con =
      client_con->pipe(Stdio.PROP_NONBLOCK | Stdio.PROP_BIDIRECTIONAL);

    SSL.File server = SSL.File(server_con, server_ctx);

    SSL.File client = SSL.File(client_con, client_ctx);

    if (!client->connect(UNDEFINED, session)) return 0;
    if (!server->accept()) return 0;

    int state;

    int trigged = 10;
    int remaining = sizeof(client_msg);

    string fail;

    string server_buf = "";
    void server_send_data() {
      trigged = 2;
      int bytes = server->write(server_buf);
      server_buf = server_buf[bytes..];
      remaining -= bytes;
      if (!sizeof(server_buf)) {
	server->set_write_callback(UNDEFINED);
	if (!remaining) {
	  server->close();
	  state = 2;
	}
      }
    };
    void server_got_data(mixed ignored, string data) {
      trigged = 2;
      if (!sizeof(server_buf)) server->set_write_callback(server_send_data);
      server_buf += data;
    };
    void server_got_close() {
      trigged = 2;
    };
    void server_alert() {
      trigged = 0;
      // server->close();
    };

    void server_check_suite() {
      if ((server->query_version() != exp_version) ||
	  (server->query_suite() != exp_suite)) {
        fail =
	  sprintf("Unexpected cipher suite (server):\n"
		  "Got: %s [%s]\n"
		  "Exp: %s [%s]\n",
		  fmt_cipher_suite(server->query_suite()),
		  fmt_version(server->query_version()),
		  fmt_cipher_suite(exp_suite),
		  fmt_version(exp_version));
	return;
      }
      server->set_write_callback(sizeof(server_buf) && server_send_data);
    };

    string client_recv_buf = "";
    string client_send_buf = client_msg;
    void client_got_data(mixed ignored, string data) {
      trigged = 2;
      client_recv_buf += data;
    };
    void client_send_data() {
      trigged = 2;
      int bytes = client->write(client_send_buf[..4095]);
      client_send_buf = client_send_buf[bytes..];
      if (!sizeof(client_send_buf)) {
	client->set_write_callback(UNDEFINED);
	state = 1;
      }
    };
    void client_got_close() {
      trigged = 2;
      if (state == 2) state = 3;
      client->close();
    };
    void client_alert() {
      trigged = 0;
      // client->close();
    };

    void client_check_suite() {
      if ((client->query_version() != exp_version) ||
	  (client->query_suite() != exp_suite)) {
        fail =
	  sprintf("Unexpected cipher suite (client):\n"
		  "Got: %s [%s]\n"
		  "Exp: %s [%s]\n",
		  fmt_cipher_suite(client->query_suite()),
		  fmt_version(client->query_version()),
		  fmt_cipher_suite(exp_suite),
		  fmt_version(exp_version));
	return;
      }
      client->set_write_callback(client_send_data);
      client_send_data();
    };

    server->set_nonblocking(server_got_data, server_check_suite,
			    server_got_close);
    server->set_alert_callback(server_alert);
    client->set_nonblocking(client_got_data, client_check_suite,
			    client_got_close);
    client->set_alert_callback(client_alert);

    // We loop the backend while something happens...
    while (!fail && trigged--) {
      mixed err = catch {
        Pike.DefaultBackend(0.005);
      };
      if (err) {
        state = -1;
        master()->handle_error(err);
        break;
      }
    }

    if (fail) {
    } else if (!exp_data ||
        ((state == 3) && (client_recv_buf == exp_data))) {
      // if (exp_data) log_status("OK: %s\n", fmt_cipher_suite(exp_suite));
    } else {
      if (state != 3) {
	fail = sprintf("Unexpected exit state: %d.\n", state);
      } else {
	fail =
	  sprintf("Unexpected result:\n"
		  "Got: %O\n"
		  "Exp: %O\n",
		  client_recv_buf[..32], exp_data[..32]);
      }
    }
    client->close();
    server->close();
    destruct(client);
    destruct(server);
    return fail;
  };
  add_constant("low_test_ssl_connection", f);
]])

test_do([[
  import SSL.Constants;
  int f(int client_min, int client_max, int server_min, int server_max,
        string exp_data, object server_ctx, array(int) suites,
        array(CertificatePair)|void client_certs)
  {
    server_ctx->min_version = server_min;
    server_ctx->max_version = server_max;

    int exp_version = server_max;
    if (exp_version > client_max) {
      exp_version = client_max;
    }
    if (obsoleted_suites[suites[0] ] &&
        (exp_version > obsoleted_suites[suites[0] ])) {
      exp_version = obsoleted_suites[suites[0] ];
    }

    if ((exp_version < server_min) ||
	(exp_version < client_min)) {
      exp_data = 0;
    }

    int exp_suite = -1;

    foreach(suites, int suite) {
      if (exp_version < PROTOCOL_TLS_1_2) {
	int ke = CIPHER_SUITES[suite ][0];
	if ((ke == KE_ecdh_rsa) || (ke == KE_dh_rsa)) {
	  // We only have self-signed certificates, so all ECDH_RSA and
	  // DH_RSA suites will fail prior to TLS 1.2, since they require
	  // the certificate to be signed with RSA.
	  continue;
	}
	if (sizeof(CIPHER_SUITES[suite ]) == 4) {
	  // AEAD ciphers and cipher specific prfs not supported prior
	  // to TLS 1.2.
	  continue;
	}
	if (client_max < PROTOCOL_TLS_1_0) {
	  // SSL 3.0 doesn't support extensions, so it can't support ECC.
	  if ((ke == KE_ecdh_ecdsa) || (ke == KE_ecdhe_ecdsa) ||
	      (ke == KE_ecdhe_rsa) || (ke == KE_ecdh_anon)) {
	    continue;
	  }
	}
      }
      exp_suite = suite;
      break;
    }

    if (exp_suite == -1) exp_data = 0;

    // A client that supports just a single cipher suite.
    SSL.Context client_ctx = TestContext();
    client_ctx->random = random_string;
    client_ctx->preferred_suites = suites;
    client_ctx->min_version = client_min;
    client_ctx->max_version = client_max;
    server_ctx->expect_fail = client_ctx->expect_fail = !exp_data;

    if (client_certs) {
      // Add the certificates.
      foreach(client_certs, CertificatePair cp) {
        client_ctx->add_cert(cp);
      }
    }

    SSL.Session session = SSL.Session();

    string fail =
      low_test_ssl_connection(server_ctx, client_ctx,
			      exp_data, exp_suite, exp_version,
			      session);

    if (fail) {
      log_ssl_failure(client_min, client_max, server_min, server_max,
		      fail, suites);

      return 0;
    }

    string sessionid = session->identity;
    if (!sessionid) return 1;

    if (!`==(client_min, client_max, server_min, server_max)) return 1;

    // Try resuming the session.

    fail =
      low_test_ssl_connection(server_ctx, client_ctx,
			      exp_data, suites[0], exp_version,
			      session);

    if (!fail && (sessionid != session->identity)) {
      fail = "New session.";
    }

    if (fail) {
      log_ssl_failure(client_min, client_max, server_min, server_max,
		      "RESUME: " + fail, suites);

      return 0;
    }
    return 2;
  };
  add_constant("test_ssl_connection", f);
]])

test_do([[
  import SSL.Constants;
  array(string) f(SSL.Context server_ctx, SSL.Context client_ctx,
		  string exp_data, int exp_suite, ProtocolVersion exp_version)
  {
#if constant(thread_create)
    Stdio.File client_con = Stdio.File();
    Stdio.File server_con =
      client_con->pipe(Stdio.PROP_NONBLOCK | Stdio.PROP_BIDIRECTIONAL);
    SSL.sslfile server;
    SSL.sslfile client;

    Thread.Thread server_thread =
      Thread.Thread(lambda(int bytes) {
        server = SSL.sslfile(server_con, server_ctx);
        server->set_blocking();
	if (!server->accept()) {
	  return exp_data && "Server accept failed.\n";
        }

        if (exp_data &&
	    ((server->query_version() != exp_version) ||
	     (server->query_suite() != exp_suite))) {
	  return
	    sprintf("Unexpected cipher suite (server):\n"
		    "Got: %s [%s]\n"
		    "Exp: %s [%s]\n",
		    fmt_cipher_suite(server->query_suite()),
		    fmt_version(server->query_version()),
		    fmt_cipher_suite(exp_suite),
		    fmt_version(exp_version));
        }
	while (bytes) {
	  // NB: Client doesn't close connection before all data has
	  //     been returned, so we can't just use read(1024) here.
	  string data = server->read(min(1024, bytes));
	  if (!data) {
	    return exp_data && "Server failed to receive all data.\n";
          }
	  bytes -= sizeof(data);
	  int sent_bytes = server->write(data);
	}
	server->close();
	return 0;
      }, sizeof(client_msg));

    /* SSL.sslfile */ client = SSL.sslfile(client_con, client_ctx);

    client->set_blocking();
    if (!client->connect()) {
      server_thread->wait();
      return exp_data ? ({ "Client failed to connect.\n" }) : ({});
    }
    if (exp_data &&
	((client->query_version() != exp_version) ||
	 (client->query_suite() != exp_suite))) {
      return ({
	sprintf("Unexpected cipher suite (client):\n"
		"Got: %s [%s]\n"
		"Exp: %s [%s]\n",
		fmt_cipher_suite(client->query_suite()),
		fmt_version(client->query_version()),
		fmt_cipher_suite(exp_suite),
		fmt_version(exp_version)),
      });
    }
    Thread.Thread sender_thread =
      Thread.Thread(lambda() {
        int bytes = client->write(client_msg);
	if (bytes == sizeof(client_msg)) return 0;
	return exp_data && "Client failed to write all data.\n";
      });

    Thread.Thread reader_thread =
      Thread.Thread(lambda() {
        string recv = client->read(sizeof(client_msg));
	client->close();
	if (recv == client_msg) {
	  if (exp_data) return 0;
	  return "Succeeded where failure expected.\n";
	}
	return exp_data && "Failed to read all data.\n";
      });

    array(string) failures = ({
      sender_thread, server_thread, reader_thread,
    })->wait() - ({ 0 });

#ifdef SSL3_DEBUG
    if (sizeof(failures)) {
      werror("Failures: %O\n", failures);
    }
#endif
    client->close();
    server->close();
    destruct(client);
    destruct(server);
    return failures;
#else /* !constant(thread_create) */
    return ({});
#endif /* constant(thread_create) */
  };
  add_constant("threaded_low_test_ssl_connection", f);
]])

test_do([[
  import SSL.Constants;
  int f(int client_min, int client_max, int server_min, int server_max,
        string exp_data, object server_ctx, array(int) suites,
        array(CertificatePair)|void client_certs)
  {
    server_ctx->min_version = server_min;
    server_ctx->max_version = server_max;

    int exp_version = server_max;
    if (exp_version > client_max) {
      exp_version = client_max;
    }
    if (obsoleted_suites[suites[0] ] &&
        (exp_version > obsoleted_suites[suites[0] ])) {
      exp_version = obsoleted_suites[suites[0] ];
    }

    if ((exp_version < server_min) ||
	(exp_version < client_min)) {
      exp_data = 0;
    }

    // We only have self-signed certificates, so all ECDH_RSA and
    // DH_RSA suites will fail prior to TLS 1.2, since they require
    // the certificate to be signed with RSA.
    if ((exp_version < PROTOCOL_TLS_1_2) && (sizeof(suites) == 1)) {
      int ke = CIPHER_SUITES[suites[0] ][0];
      if ((ke == KE_ecdh_rsa) || (ke == KE_dh_rsa)) {
        exp_data = 0;
      }
      if (sizeof(CIPHER_SUITES[suites[0] ]) == 4) {
        // AEAD ciphers and cipher specific prfs not supported prior
	// to TLS 1.2.
	exp_data = 0;
      }
      if (client_max < PROTOCOL_TLS_1_0) {
        // SSL 3.0 doesn't support extensions, so it can't support ECC.
	if ((ke == KE_ecdh_ecdsa) || (ke == KE_ecdhe_ecdsa) ||
	    (ke == KE_ecdhe_rsa) || (ke == KE_ecdh_anon)) {
	  exp_data = 0;
	}
      }
    }

    // A client that supports just a single cipher suite.
    SSL.Context client_ctx = TestContext();
    client_ctx->random = random_string;
    client_ctx->preferred_suites = suites;
    client_ctx->min_version = client_min;
    client_ctx->max_version = client_max;
    server_ctx->expect_fail = client_ctx->expect_fail = !exp_data;

    if (client_certs) {
      // Add the certificates.
      foreach(client_certs, CertificatePair cp) {
        client_ctx->add_cert(cp);
      }
    }

    array(string) failures =
      threaded_low_test_ssl_connection(server_ctx, client_ctx,
				       exp_data, suites[0], exp_version);

    if (!sizeof(failures)) return 1;

    log_ssl_failure(client_min, client_max, server_min, server_max,
		    failures * "\n", suites);
    return 0;
  };
  add_constant("threaded_test_ssl_connection", f);
]])

test_do([[
  // Default suite for TLS 1.1 and earlier.
  // Default to just testing all key exchanges with a cipher
  // suite that is mostly valid in all versions prior to TLS 1.3.
  add_constant("legacy_suites", ({
    // These two are used to test cipher suite protocol downgrade.
    SSL.Constants.SSL_rsa_export_with_des40_cbc_sha,	// <= TLS 1.0.
    SSL.Constants.SSL_rsa_with_des_cbc_md5,		// <= TLS 1.1.

    SSL.Constants.SSL_dh_anon_with_3des_ede_cbc_sha,
    SSL.Constants.SSL_rsa_with_3des_ede_cbc_sha,
    SSL.Constants.SSL_dhe_rsa_with_3des_ede_cbc_sha,
    SSL.Constants.SSL_dhe_dss_with_3des_ede_cbc_sha,
    SSL.Constants.SSL_dh_rsa_with_3des_ede_cbc_sha,
    SSL.Constants.SSL_dh_dss_with_3des_ede_cbc_sha,
#if constant(Crypto.ECC.Curve)
    SSL.Constants.TLS_ecdh_anon_with_3des_ede_cbc_sha,
    SSL.Constants.TLS_ecdhe_rsa_with_3des_ede_cbc_sha,
    SSL.Constants.TLS_ecdhe_ecdsa_with_3des_ede_cbc_sha,
    SSL.Constants.TLS_ecdh_rsa_with_3des_ede_cbc_sha,
    SSL.Constants.TLS_ecdh_ecdsa_with_3des_ede_cbc_sha,
#endif
  }));

  // In TLS 1.2 AEAD suites were added, and in
  // TLS 1.3 all other suites will be removed.
  // Try some AEAD suites that are available with most versions of Nettle.
  add_constant("aead_suites", ({
    SSL.Constants.TLS_dhe_rsa_with_aes_128_ccm,
#if constant(Crypto.ECC.Curve)
    SSL.Constants.TLS_ecdhe_ecdsa_with_aes_128_ccm,
#endif
#if constant(Crypto.AES.GCM)
    SSL.Constants.TLS_dh_anon_with_aes_128_gcm_sha256,
    SSL.Constants.TLS_dhe_dss_with_aes_256_gcm_sha384,
#if constant(Crypto.ECC.Curve)
    SSL.Constants.TLS_ecdhe_rsa_with_aes_256_gcm_sha384
#endif
#endif
  }));
]])

dnl client_min, client_max, server_min, server_max, exp
test_tests([[
array(int) a() {
  int successes;
  int tests;
  int skip;

  for (int server_max = SSL.Constants.PROTOCOL_SSL_3_0;
       server_max <= SSL.Constants.PROTOCOL_TLS_MAX; server_max++) {
  for (int server_min = SSL.Constants.PROTOCOL_SSL_3_0;
       server_min <= server_max; server_min++) {
  for (int client_max = SSL.Constants.PROTOCOL_SSL_3_0;
       client_max <= SSL.Constants.PROTOCOL_TLS_MAX; client_max++) {
  for (int client_min = SSL.Constants.PROTOCOL_SSL_3_0;
       client_min <= client_max; client_min++) {

    int exp_protocol = min(client_max, server_max);

    string exp_res = client_msg;
    if ((client_max < server_min) || (server_max < client_min)) {
      exp_res = UNDEFINED;
    }

    array(int) suites = legacy_suites;

    if ((server_max >= SSL.Constants.PROTOCOL_TLS_1_2) ||
	(client_max >= SSL.Constants.PROTOCOL_TLS_1_2)) {
      // Try some AEAD suites too.
      if (client_min >= SSL.Constants.PROTOCOL_TLS_1_3) {
        // None of the legacy suites are valid in TLS 1.3 (not AEAD),
	// and will never be sent by a TLS 1.3-only client.
	suites = ({});
      }
      suites += aead_suites;
    }

#if 0
    if( `==(client_min, server_min, SSL.Constants.PROTOCOL_SSL_3_0) &&
	(client_max == server_max) ) {
      // Test all known cipher suites.
      suites = server_ctx->get_suites(-1, 2);
    }
#endif

#if constant(thread_create)
    log_status("Testing %s..%s client with %s..%s server (threaded)...",
	       SSL.Constants.fmt_version(client_min),
	       SSL.Constants.fmt_version(client_max),
	       SSL.Constants.fmt_version(server_min),
	       SSL.Constants.fmt_version(server_max));

    foreach(suites, int suite) {
      int count = threaded_test_ssl_connection(client_min, client_max,
					       server_min, server_max, exp_res,
					       server_ctx, ({suite}) );
      successes += count;
      tests += count || 1;
    }
#else
    skip += sizeof(suites);
#endif /* constant(thread_create) */

    log_status("Testing %s..%s client with %s..%s server...",
	       SSL.Constants.fmt_version(client_min),
	       SSL.Constants.fmt_version(client_max),
	       SSL.Constants.fmt_version(server_min),
	       SSL.Constants.fmt_version(server_max));

    if( `==(client_min, server_min, SSL.Constants.PROTOCOL_SSL_3_0) &&
	(client_max == server_max) ) {
      // Test all known cipher suites.
      suites = server_ctx->get_suites(-1, 2);
    }

    foreach(suites, int suite) {
      int count = test_ssl_connection(client_min, client_max,
				      server_min, server_max, exp_res,
				      server_ctx, ({suite}) );
      successes += count;
      tests += count || 1;
      if (!count) skip += !!exp_res;	// Skipped resume.
    }
  }}}}
  return ({ successes, tests-successes, skip });
}
]])

cond_resolv(Crypto.ECC, [[

test_tests([[
object make_context(Crypto.ECC.Curve curve) {
  SSL.Context ctx = TestContext();
  ctx->random = random_string;
  Crypto.Sign ecdsa =
    curve->ECDSA()->set_random(random_string)->generate_key();
  mapping attrs = ([
    "organizationName" : "Test",
    "commonName" : "*",
  ]);
  string cert = Standards.X509.make_selfsigned_certificate(ecdsa,
    3600*24, attrs);

  ctx->add_cert(ecdsa, ({ cert }));

  ctx->preferred_suites = server_ctx->get_suites(-1, 2);
  return ctx;
}
array(int) a() {
  int successes;
  int tests;

  foreach( ({
    Crypto.ECC.SECP_192R1,
    Crypto.ECC.SECP_224R1,
    Crypto.ECC.SECP_256R1,
    Crypto.ECC.SECP_384R1,
    // Crypto.ECC.SECP_521R1, // Tested above.
  }), Crypto.ECC.Curve curve)
  {
    log_status("Testing ECDSA curve %O...", curve->name());
    object server_ctx = make_context(curve);
    array suites = server_ctx->get_suites(128, 2);
    int count = test_ssl_connection(0x300, 0x303, 0x303, 0x303,
				    client_msg,server_ctx,suites);
    successes += count;
    tests += count || 1;
  }

  return ({ successes, tests-successes });
}
]])

dnl client_ver, client_bits, client_strict,
dnl server_ver, server_bits, server_strict, exp_suite
define(test_suite_b, [[
  test_program([[
    import SSL.Constants;

    int a() {
      int exp_protocol = min($2, $5);

      log_status("Testing Suite B with %d bit %s client and %d bit %s server...",
		 $2, fmt_version($1), $5, fmt_version($4));

      server_ctx->configure_suite_b($5, $6);
      server_ctx->max_version = $4;

      SSL.Context client_ctx = TestContext();
      client_ctx->random = random_string;
      client_ctx->configure_suite_b($2, $3);
      client_ctx->max_version = $1;

      int exp_suite = $7;
      server_ctx->expect_fail = client_ctx->expect_fail = !exp_suite;

      int exp_version = server_ctx->max_version;
      if (exp_version > client_ctx->max_version) {
        exp_version = client_ctx->max_version;
      }
      if (obsoleted_suites[exp_suite] &&
          (exp_version > obsoleted_suites[exp_suite])) {
        exp_version = obsoleted_suites[exp_suite];
      }

      if ((exp_version < server_ctx->min_version) ||
	  (exp_version < client_ctx->min_version)) {
	exp_version = 0;
	exp_suite = 0;
      }

      string fail =
	low_test_ssl_connection(server_ctx, client_ctx, exp_suite && client_msg,
				exp_suite, exp_version);
      if (fail && exp_suite) {
	log_ssl_failure(client_ctx->min_version, client_ctx->max_version,
			server_ctx->min_version, server_ctx->max_version,
			fail, server_ctx->preferred_suites);
	return 0;
      }
      return 1;
    };
  ]])
]])

test_suite_b(0x303, 192, 2, 0x303, 192, 2,
	     TLS_ecdhe_ecdsa_with_aes_256_gcm_sha384)
test_suite_b(0x303, 192, 2, 0x303, 128, 2,
	     TLS_ecdhe_ecdsa_with_aes_256_gcm_sha384)
test_suite_b(0x303, 128, 2, 0x303, 192, 2,
	     TLS_ecdhe_ecdsa_with_aes_256_gcm_sha384)
test_suite_b(0x303, 128, 2, 0x303, 128, 2,
	     TLS_ecdhe_ecdsa_with_aes_128_gcm_sha256)
test_suite_b(0x302, 192, 1, 0x302, 192, 1,
	     TLS_ecdhe_ecdsa_with_aes_256_cbc_sha)
test_suite_b(0x302, 192, 1, 0x302, 128, 1,
	     TLS_ecdhe_ecdsa_with_aes_256_cbc_sha)
test_suite_b(0x302, 128, 1, 0x302, 192, 1,
	     TLS_ecdhe_ecdsa_with_aes_256_cbc_sha)
test_suite_b(0x302, 128, 1, 0x302, 128, 1,
	     TLS_ecdhe_ecdsa_with_aes_128_cbc_sha)

]]) dnl Crypto.ECC

test_tests([[
  import SSL.Constants;

  array(int) a() {
    int successes;
    int tests;
    array suites = server_ctx->get_suites(128, 2);
    server_ctx->preferred_suites = suites;

    // First without any certs.
    foreach(({ AUTHLEVEL_none, AUTHLEVEL_ask, AUTHLEVEL_require, }),
	    server_ctx->auth_level) {
      log_status("Client certificates: NONE, %s",
		 fmt_constant(server_ctx->auth_level, "AUTHLEVEL"));

      string exp = client_msg;
      if (server_ctx->auth_level == AUTHLEVEL_require) exp = UNDEFINED;
      for (int ver = PROTOCOL_SSL_3_0; ver <= PROTOCOL_TLS_MAX; ver++) {
	int count = test_ssl_connection(PROTOCOL_SSL_3_0, ver,
					PROTOCOL_SSL_3_0, PROTOCOL_TLS_MAX,
					exp, server_ctx, suites);
	successes += count;
	tests += count || 1;
      }
    }

    // Then with certs, but none accepted.
    array(CertificatePair) certs = server_ctx->get_certificates();
    foreach(({ AUTHLEVEL_none, AUTHLEVEL_ask, AUTHLEVEL_require, }),
	    server_ctx->auth_level) {
      log_status("Client certificates: INVALID, %s",
		 fmt_constant(server_ctx->auth_level, "AUTHLEVEL"));

      string exp = client_msg;
      if (server_ctx->auth_level == AUTHLEVEL_require) exp = UNDEFINED;
      for (int ver = PROTOCOL_SSL_3_0; ver <= PROTOCOL_TLS_MAX; ver++) {
	int count = test_ssl_connection(PROTOCOL_SSL_3_0, ver,
					PROTOCOL_SSL_3_0, PROTOCOL_TLS_MAX,
					exp, server_ctx, suites);
	successes += count;
	tests += count || 1;
      }
    }

    // Then with accepted certs, one at a time.
    foreach(certs, CertificatePair cp) {
      server_ctx->set_authorities(({ cp->certs[0] }));
      foreach(({ AUTHLEVEL_none, AUTHLEVEL_ask, AUTHLEVEL_require, }),
		 server_ctx->auth_level) {
	log_status("Client certificates: %s(%d bits), %s",
		   fmt_constant(cp->cert_type, "AUTH"),
		   cp->key->key_size(),
		   fmt_constant(server_ctx->auth_level, "AUTHLEVEL"));

	string exp = client_msg;
	for (int ver = PROTOCOL_SSL_3_0; ver <= PROTOCOL_TLS_MAX; ver++) {
	  int count = test_ssl_connection(PROTOCOL_SSL_3_0, ver,
					  PROTOCOL_SSL_3_0, PROTOCOL_TLS_MAX,
					  exp, server_ctx, suites);
	  successes += count;
	  tests += count || 1;
	}
      }
    }

    return ({ successes, tests-successes });
  }
]])

dnl FIXME: Test TLS 1.2 and earlier server and client (separately)
dnl	   without EXTENSION_encrypt_then_mac.
dnl Hmm... This might be already be tested with SSL 3.0 (client-side only).

test_do([[
  add_constant("aead_suites");
  add_constant("legacy_suites");
  add_constant("obsoleted_suites");
  add_constant("log_ssl_failure");
  add_constant("client_msg");
  add_constant("server_ctx");
  add_constant("cert_md5");
  add_constant("cert_sha1");
  add_constant("pem_cert");
  add_constant("pem_key");
  add_constant("threaded_test_ssl_connection");
  add_constant("threaded_low_test_ssl_connection");
  add_constant("test_ssl_connection");
  add_constant("low_test_ssl_connection");
  add_constant("TestContext");
]])

cond_end

dnl   test_do([[
dnl     object p = Process.Process(RUNPIKE + " \"]]SRCDIR[[/https.pike\"");
dnl     sleep(5); /* Wait a little for the server to startup */
dnl     Protocols.HTTP.Query q = Protocols.HTTP.get_url("https://localhost:25678");
dnl     if (q->status != 200 || search("html", q->data()) < 0)
dnl       error("Failed\n");
dnl     /* How to kill the process if running on windows? */
dnl     p->kill(15);
dnl   ]])

test_do( add_constant("S") )

END_MARKER
